System -msglog model

Request clientRequest : clientRequest(X) // X = in
Reply enter : enter(SLOTNUM) // SLOTNUM=0/1/2/3/4/5/6 

Request carenter : carenter(SLOTNUM) // SLOTNUM=1/2/3/4/5/6
Reply receipt : receipt(TOKENID)

Dispatch outTokenid : outTokenid(TOKENID)
Dispatch weightData : weightData(W) 

Dispatch move : move(X,Y)
Dispatch moveok   : move( MOVESHORT )

Context ctxparkmanager ip [host="localhost" port=8070]

QActor client context ctxparkmanager {
	[# var TOKENID = ""
	 var SLOTNUM = 0  #]
	State s0 initial {
		println("Starting Client.")
	}
	Goto requestIn
	
	State requestIn {
		request park_manager_service -m clientRequest : clientRequest(in)
	}
	Transition t0 whenTime 5000 -> end
				  whenReply enter -> handleSlotnum
		
	State handleSlotnum {
		onMsg( enter : enter(SLOTNUM)) {
			[# SLOTNUM = payloadArg(0).toInt() #]
		} 
	}
	Goto end if [# SLOTNUM==0 #] else moveIndoor
	
	State moveIndoor {
		request park_manager_service -m carenter : carenter($SLOTNUM)	
	}
	Transition t1 whenReply receipt -> handleTokenid
	
	State handleTokenid {
		printCurrentMessage
		onMsg( receipt : receipt(TOKENID)) {
			[# TOKENID = "${payloadArg(0)}" #]
		}
	}
	Transition out whenTime 6000 -> requestOut
	
	State requestOut {
		forward park_manager_service -m outTokenid : outTokenid($TOKENID) 
	}
	Goto end

	State end {
		println("Client finished")
	}
} 

QActor park_manager_service context ctxparkmanager {
	[# 
		var counter=0 
		var FREE_INDOOR=true
		var x = 0
		var y = 0
	#]
	State s0 initial {
		discardMsg Off
		solve( consult("parking.pl"))
		solve( dynamic("freeSlot/1") )
		solve(unoccupySlot(1))
		qrun coap.actorQakWeightCoapObserver.activate( myself  )
		println("Starting ParkManagerService.")
	}
	Goto accept
	
	State accept {
		println("ParkManagerServing accepting client requests...")	
	}
	Transition clientMsg whenRequest clientRequest -> handleClientRequest
						 whenRequest carenter -> handleCarEnter
						 whenMsg outTokenid -> handleClientOut
	
	State handleClientRequest {
		println("ParkManagerService handling client request")
		onMsg( clientRequest : clientRequest(X)) {
			[# val requestType = "${payloadArg(0)}" #]
			if [# requestType == "in" #] {
				
				if [# coap.actorQakWeightCoapObserver.readResponse()=="free" #] {
				
					solve( getFreeSlot( S ) )
					[# val SLOTNUM = getCurSol("S") #] 
					solve( occupySlot( $SLOTNUM ) ) // let's assume at the moment that the client is going to do carenter
				
					replyTo clientRequest with  enter :  enter( $SLOTNUM )	
				}
				else {
					replyTo clientRequest with  enter :  enter( 0 ) //at the moment the client is refused if the indoor area is occupied
				}
			}
		}
	}
	Goto accept
	
	State handleCarEnter {
		printCurrentMessage
		onMsg(carenter : carenter(SLOTNUM)) {
			[# val SLOTNUM = payloadArg(0).toInt() #]
			
			updateResource [# ""+SLOTNUM #]
			
			solve( indoor(X,Y) )
			[# x = getCurSol("X").toString().toInt() #]
			[# y = getCurSol("Y").toString().toInt() #]
			
			//forward transport_trolley -m move : move($x,$y)
			[# forward("move", "move($x,$y)" ,"transport_trolley" ) #]
			
			
			solve( getCoordinates($SLOTNUM,X,Y) )
			[# x = getCurSol("X").toString().toInt() #]
			[# y = getCurSol("Y").toString().toInt() #]

			//forward transport_trolley -m move : move($x,$y)
			[# forward("move", "move($x,$y)" ,"transport_trolley" ) #]
					
			[# val TOKENID = "$SLOTNUM"+"$counter" #]
			[# counter++ #]
					
			replyTo carenter with  receipt :  receipt( $TOKENID )
		}
	}
	Transition t3 whenTime 1000 -> moveTrolleyHome
						 whenRequest clientRequest -> handleClientRequest
						 whenRequest carenter -> handleCarEnter
						 whenMsg outTokenid -> handleClientOut
	
	State handleClientOut {
		println("ParkManagerServing out.")
		
		onMsg( outTokenid : outTokenid(X)) {
			[# val TOKENID = "${payloadArg(0)}" #]
			[# val SLOTNUM =  TOKENID.first() #]

			updateResource [# ""+TOKENID #]
			
			solve( getCoordinates($SLOTNUM,X,Y) )
			[# x = getCurSol("X").toString().toInt() #]
			[# y = getCurSol("Y").toString().toInt() #]
			
			//forward transport_trolley -m move : move($x,$y)
			[# forward("move", "move($x,$y)" ,"transport_trolley" ) #]
			
			solve( outdoor(X,Y) )
			[# x = getCurSol("X").toString().toInt() #]
			[# y = getCurSol("Y").toString().toInt() #]
			
			//forward transport_trolley -m move : move($x,$y)
			[# forward("move", "move($x,$y)" ,"transport_trolley" ) #]
			
			solve( unoccupySlot( $SLOTNUM ) ) 
		}
	}
	Transition t4 whenTime 1000 -> moveTrolleyHome
				  whenRequest clientRequest -> handleClientRequest
				  whenRequest carenter -> handleCarEnter
				  whenMsg outTokenid -> handleClientOut
				  
	State moveTrolleyHome {
		solve( home(X,Y) )
		[# x = getCurSol("X").toString().toInt() #]
		[# y = getCurSol("Y").toString().toInt() #]
		//forward transport_trolley -m move : move($x,$y)
		[# forward("move", "move($x,$y)" ,"transport_trolley" ) #]
	}
	Goto accept
	 
}


QActor weight_sensor context ctxparkmanager {
	[#   
		var prevWeight=0f
		
		fun resourceInfo(weight: Float, preWeight: Float) : String {
			if (Math.abs(weight - prevWeight) > 10) {
				if (weight.toInt() == 0) {
					return "free"
				}
 			}
 			return "occupied"
 		}
	#]
	
	State s0 initial {
		println("Starting Weight sensor.")
		updateResource [# "free" #] //stato iniziale
	}
	Goto wait
	
	State wait {
		println("Weightsensor waiting for data...")
	}
	Transition data whenMsg weightData -> handleData
	
	State handleData {
		println("Weightsensor received data")
		onMsg(weightData : weightData(W)) {
			[# var weight = "${payloadArg(0)}" #]
			println("weight: $weight")
			updateResource [# resourceInfo(weight.toFloat(), prevWeight.toFloat()) #]
			[# prevWeight = weight.toFloat() #]
		}
	}
	Goto wait
	
}


QActor transport_trolley context ctxparkmanager {
	
	[#
		val mapname     = "parkingMap"  		 
		var Myself      = myself   
		var CurrentPlannedMove = "" 
		var x = ""
		var y = ""
	#]
	
	State s0 initial {
		run itunibo.planner.plannerUtil.initAI()
		run itunibo.planner.plannerUtil.loadRoomMap("$mapname") 
		run itunibo.planner.plannerUtil.showMap( )   
     	run itunibo.planner.plannerUtil.showCurrentRobotState()
     	
     	run pathexecutil.register( Myself )
	}
	Goto wait
	
	State wait {
		println("Waiting messages...")
	}
	Transition t0 whenMsg move -> handle
	
	State handle {
		printCurrentMessage
		onMsg(move : move(X,Y)) {
			[# 
				x = payloadArg(0)
				y = payloadArg(1)
			#]
			println("x: $x, y: $y")
			run itunibo.planner.plannerUtil.planForGoal(x,y)
		}
	}
	Goto execPlannedMoves     
    
	State execPlannedMoves{   
		delay 300
 		[#  CurrentPlannedMove = itunibo.planner.plannerUtil.getNextPlannedMove() #]
 		println("+++++++++++++++++++++++++++++++ $CurrentPlannedMove")
 	}
 	Goto doMove if [# CurrentPlannedMove.length>0 #] else parkthecar
 
 	State doMove{ 		
 	}
 	Goto wMove if [# CurrentPlannedMove == "w" #] else turnMove
 	
 	State wMove {     
 		qrun pathexecutil.doMove(myself, "p" )  
 	}
 	Transition t0 whenMsg moveok   -> stepDone   
  	
 	 State stepDone{  
 	 	printCurrentMessage
 		//updateResource [# itunibo.planner.plannerUtil.getMap() #]
 		run itunibo.planner.plannerUtil.updateMap(  "w" )
 		run itunibo.planner.plannerUtil.showCurrentRobotState( )
 		//updateResource [# "stepDone" # ]
  	} 
 	Goto execPlannedMoves
 	
 	State turnMove{
 		println("-----qqqq-----------------dsaasds---------------sdaadssdasdassdadsd - -- -$CurrentPlannedMove")
  		if [# CurrentPlannedMove == "l" || CurrentPlannedMove == "r"  #]{
  			qrun pathexecutil.doMove(myself, "$CurrentPlannedMove")
  		} 
 	}   
  	Transition t0  whenMsg moveok   -> rotationDone


 	State rotationDone{
 		run itunibo.planner.plannerUtil.updateMap(  "$CurrentPlannedMove" )
   		//run itunibo.planner.plannerUtil.showCurrentRobotState( )
 	} 
 	 Goto execPlannedMoves
 	
 	State parkthecar{
 		printCurrentMessage
 		run itunibo.planner.plannerUtil.showCurrentRobotState( )  
 		if [# ! itunibo.planner.plannerUtil.atPos(x.toInt(),y.toInt()) #]{
 			run itunibo.planner.plannerUtil.planForGoal(x,y)
 		}
 		else {
 			[# println(itunibo.planner.plannerUtil.get_curPos().toString()) #]
 			updateResource [# itunibo.planner.plannerUtil.get_curPos().toString() #]
 		}
 	}
    Goto execPlannedMoves  if[# ! itunibo.planner.plannerUtil.atPos(x.toInt(),y.toInt()) #] else  wait
	
}


/*
QActor weight_mock context ctxparkmanager {
	State s0 initial {
		println("Starting WeightMock")
	} Goto sendData
	
	State sendData {
		forward weight_sensor -m weightData : weightData(50)
		delay 5000
		forward weight_sensor -m weightData : weightData(45)
		delay 5000
		forward weight_sensor -m weightData : weightData(0)
	}
}
*/