/*
 * ----------------------------------------------------------------------
 * RUNS ON Raspberry
 *
 * - loads the kb about the system (sonar2021Kb.pl) and showSystemConfiguration
 * - loads the sonar2021ConfigKb.pl and reads the sonar-data source (real or simulated).
 *   	If real, activates the CodedQActor sonardatasource of class sonarHCSR04Support2021
 *   	If simulated, activates the CodedQActor sonarsimulator of class sonarsimulator
 * - setup the sonar-data pipe that emits-on-stream the event sonar:distance( V )
 * - handles the event sonar and emits the event sonarrobot:sonar( D )
 * 
 * TO INTERACT from Rasp to PC without MQTT => 
 *            remove Windows firewall or allow port 8028
 * ----------------------------------------------------------------------
 */

 	//"tcp://mqtt.eclipse.org:1883"
	//mqtt.eclipse.org
	//tcp://test.mosquitto.org
	//mqtt.fluux.io
	//"tcp://broker.hivemq.com" 

System -msglog  sonarnaive   
//mqttBroker "broker.hivemq.com" :  1883 eventTopic "sonar/data"

Dispatch sonarstart      : sonarstart(V)  
Dispatch simulatorstart  : simulator(V)  
Event    sonar           : distance( V )   //emitted by rx.sonarSimulator or sensors.sonarHCSR04SupportActor	
Event    sonarrobot      : sonar( V )      //for the application level
 
Context ctxsonar         ip [host="localhost"     port=8068   ] 	 	
Context ctxsonarresource ip [host= "127.0.0.1"    port=8028   ]
  
//DATA SOURCES
CodedQActor sonarsimulator  context ctxsonar className "sonarSimulator"  
CodedQActor sonardatasource context ctxsonar className "sonarHCSR04Support2021"

//Filters
//CodedQActor datalogger     context ctxsonar className "dataLogger"
CodedQActor datacleaner    context ctxsonar className "dataCleaner"
//CodedQActor distancefilter context ctxsonar className "distanceFilter"

ExternalQActor sonarresource context ctxsonarresource
 
QActor sonar context ctxsonar {     
[# var simulate = true
   lateinit var firstActorInPipe : ActorBasic 
#]
 	State s0 initial {  		 
 		println("sonar START")
 		discardMsg On		 		/*
 		 * INTROSPECTION
 		 */
 		solve( consult("sysRules.pl")	 )
 		solve( consult("sonarnaive.pl")       )
 		solve( showSystemConfiguration   )
 		/*
 		 * CONFIGURATION
 		 */
 		solve( consult("sonar2021ConfigKb.pl")	 )
 		solve( simulate(X) )
		println( currentSolution )	//yes X / on oppure X / off
		[# val x = getCurSol("X").toString() 
		   simulate = ( x == "on")	
		   println( "simulate=$simulate" )
		#] 		

		//CREATE THE PIPE
		[#  if( simulate ) firstActorInPipe = sysUtil.getActor("sonarsimulator")!!  //generates simulated data
			else firstActorInPipe           = sysUtil.getActor("sonardatasource")!!  //generates REAL data
 			firstActorInPipe.
				subscribeLocalActor("datacleaner"). 		//removes 'wrong' data''
				//subscribeLocalActor("datalogger").		    //logs (shows) the data generated by the sonar
  				//subscribeLocalActor("sonar").				//handles sonarrobot but does not propagate ...
 				//subscribeLocalActor("distancefilter").		//propagates the lcoal stream event obstacle
  				subscribeLocalActor("sonar")  
		#]    
  
 		//ACTIVATE THE DATA SOURCE (REAL or SIMULATED)
		if [# simulate #]{forward sonarsimulator -m simulatorstart : simulatorstart(ok) }
 		else{ forward sonardatasource -m sonarstart : sonarstart(ok) }  //payload don't care

  	}  
  	Transition t0  whenEvent sonar -> handleSonarData  
					  
 	
  	State handleSonarData{
  		printCurrentMessage
  		 onMsg( sonar : distance(D) ){
  		 	[# val d = payloadArg(0) 
  		 	   val ev = MsgUtil.buildEvent(name,"sonarrobot","sonar($d)")
  		 	   emit( ev, false  )  //Not emit for me (but not applies to MQTT)
  		 	#]  
  		 }
  	}
   	Transition t0 whenEvent sonar -> handleSonarData
 }

